#!/bin/zsh

# This script is supposed to be sourced and not executed.
# Typically one add `source /path/to/brzl_init` inside ~/.bashrc or ~/.zshrc
([[ -n $ZSH_EVAL_CONTEXT && $ZSH_EVAL_CONTEXT =~ :file$ ]] ||
 [[ -n $BASH_VERSION ]] && (return 0 2>/dev/null)) || {
    >&2 echo "[E] $0 has to be sourced."; exit 1;
}

# Only load this file once
[[ -z "${BREZEL_INITIALIZED}" ]] && BREZEL_INITIALIZED=true || return

# Main function for brezel initialization
brezel_init() {
    # Find out the path of the present script
    local this_script
    [[ -n $BASH_VERSION ]] \
        && this_script="${BASH_SOURCE[0]}" \
        || this_script="${(%):-%x}"

    # Brezel Global Variables
    BREZEL_INSTALL_DIR=$(cd "$(dirname "${this_script}")" && pwd -P)
    BREZEL_CLI_INIT="${this_script##*/}"
    BREZEL_CLI="${BREZEL_CLI_INIT%_init}"
    BREZEL_ROOT=$(cd "${BREZEL_INSTALL_DIR}/../.." && pwd -P)
    BREZEL_HOME="${HOME}/.brezel"
    BREZEL_TMPDIR="${BREZEL_HOME}/tmp"
    BREZEL_WORKSPACE="${BREZEL_WORKSPACE:-${HOME}/ResearchProjects}"
    BREZEL_RC="${BREZEL_HOME}/etc/brezelrc"

    # Create home directories
    mkdir -p "${BREZEL_HOME}"
    mkdir -p "${BREZEL_HOME}/cache"
    mkdir -p "${BREZEL_HOME}/log"
    mkdir -p "${BREZEL_HOME}/etc/secrets"
    [[ -L "${BREZEL_HOME}/secrets" ]] || ln -sf "etc/secrets" "${BREZEL_HOME}/secrets"
    mkdir -p "${BREZEL_TMPDIR}"

    # Initialize global config
    [[ -f "${BREZEL_RC}" ]] || brezel_create_global_rc
}

# Initialize global config file
brezel_create_global_rc () {
    cat > "${BREZEL_RC}" <<-EOF
	#!/bin/zsh
	# [default]
	# [run]
	export BREZEL_RUN_PREPEND_ARGS=''
	export BREZEL_RUN_APPEND_ARGS=''
	EOF
}

# Initialize
brezel_init
export BREZEL_HOME BREZEL_WORKSPACE BREZEL_ROOT BREZEL_CLI BREZEL_TMPDIR BREZEL_RC
alias ${BREZEL_CLI}="source '${BREZEL_INSTALL_DIR}/${BREZEL_CLI}'"
unset -f brezel_init
unset -f brezel_create_global_rc

# Define function _brezel_colorize
# Only zsh is supported for now
command -v autoload >/dev/null || return 0
if autoload -Uz colors; then
    colors
    function _brezel_colorize () {
        true;
    }
fi
